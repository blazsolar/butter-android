apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/gen/**'

    if (project.hasProperty("CHECKSTYLE_MAX_ERRORS")) {
        maxErrors(CHECKSTYLE_MAX_ERRORS as int)
    }

    reports {
        html {
            destination project.provider {
                project.file "${project.buildDir}/reports/checkstyle/checkstyle.html"
            }
        }
    }

    classpath = files()

    project.android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test") && !sourceSet.name.startsWith("androidTest")) {
            source sourceSet.java.srcDirs
        }
    }
}

check.dependsOn('checkstyle')
